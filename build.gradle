/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.3.1/userguide/tutorial_java_projects.html
 */

// apply plugins
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'idea'

// package config params
group = 'ru.mihaly4'
version = '1.0.1'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
}

dependencies {
    // compile fileTree(dir: 'libs', include: '*.jar')

    implementation 'com.google.guava:guava:24.1-jre'
    implementation 'com.intellij:annotations:+@jar'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.google.dagger:dagger:2.15'
    implementation 'de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.6'
    implementation 'org.jsoup:jsoup:1.11.2'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'

    testImplementation 'junit:junit:4.12'
    // testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
}

// Define the main class for the application
mainClassName = 'ru.mihaly4.vkmd.Bootstrap'

// Configuration for jar file
jar {
    baseName = 'vkmd'

    manifest {
        attributes 'Main-Class': mainClassName,
                'Class-Path': sourceSets.main.runtimeClasspath.join(' ')
    }
}

task copyDependencies(type: Copy) {
    from sourceSets.getByName('main').compileClasspath
    into libsDir
}

task deploy(type: Exec, dependsOn: [assemble, copyDependencies]) {
    group = 'Deploy'
    description = 'Packaging your application in the executable file'

    def nativeType = ''
    def icon = ''

    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        nativeType = 'exe'
    }
    if (System.properties['os.name'].toLowerCase().contains('mac')) {
        nativeType = 'dmg'
        icon = "mac/app.icns"
    }
    if (System.properties['os.name'].toLowerCase().contains('linux')) {
        nativeType = 'deb'
    }

    def dependencies = []
    sourceSets.getByName('main').compileClasspath.forEach({file ->
        dependencies.add('-srcfiles')
        dependencies.add(file.getName())
    })

    workingDir project.projectDir
    def primaryArgs = [
            'javapackager',
            '-deploy',
            '-nosign',
            '-native', nativeType,
            '-outdir', "${buildDir}/distribution",
            '-outfile', project.name,
            '-vendor', project.group,
            '-name', 'VK Music Downloader',
            '-appclass', mainClassName,
            '-srcdir', libsDir,
            '-srcfiles', jar.archiveName,
            '-BappVersion=' + version,
            '-Bicon=' + (icon == '' ? '' : "${rootDir}/resources/deploy/${icon}")
    ] as List<String>
    commandLine = primaryArgs + dependencies
}
